plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1'
description = 'Automation project with Spring Boot and Rest Assured'

/*
 * Build metadata (can be overridden from Jenkins with -P...)
 */
ext {
	currentVersion = project.findProperty("componentVersion") ?: version
	commitIdShort  = project.findProperty("gitCommitShort") ?: "unknown"
	commitId       = project.findProperty("gitCommitFull")  ?: "unknown"
	branchName     = project.findProperty("gitBranch")      ?: "local"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

test {
	useTestNG()
}

bootJar {
	from sourceSets.test.output
	archiveFileName = "my-automation.jar"

	manifest {
		attributes(
				"Main-Class": "com.example.my_automation.MyAutomationApplication",
				"Implementation-Version": currentVersion,
				"Git-Commit-Id-Short"   : commitIdShort,
				"Git-Commit-Id-Full"    : commitId,
				"Git-Branch-Name"       : branchName
		)
	}
}

bootJar.dependsOn testClasses


dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.1.0"
	}
}

dependencies {
	// Spring Boot Web (endpoint trigger)
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//Consul integration
	implementation 'org.springframework.cloud:spring-cloud-starter-consul-config'

	// JPA + Hibernate
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// PostgreSQL
	implementation 'org.postgresql:postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Liquibase
	implementation 'org.springframework.boot:spring-boot-starter-liquibase'

	// Test execution
	implementation 'org.testng:testng:7.9.0'
	implementation 'io.rest-assured:rest-assured:5.4.0'

	// Elasticsearch
	implementation 'co.elastic.clients:elasticsearch-java:8.8.2'

	//Reporting
	implementation 'io.qameta.allure:allure-testng:2.25.0'

	// Utilities
	implementation 'com.google.code.gson:gson'
	//implementation 'org.slf4j:slf4j-api'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	//Spring Cloud OpenFeign
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'\

	implementation 'org.json:json:20231013'

	implementation 'org.jetbrains:annotations:24.1.0'

	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	//Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}